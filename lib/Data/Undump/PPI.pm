#!perl
package Data::Undump::PPI;
use warnings;
use strict;

our $VERSION = '0.05';

=head1 Name

Data::Undump::PPI - Perl extension for limited undumping of data structures
(via PPI, not eval)

=head1 Synopsis

=for comment
Remember to test this by copy/pasting to/from 91_author_pod.t

 use Data::Dumper;
 use Data::Undump::PPI;             # exports the "Undump()" function
 $Data::Dumper::Purity=1;           # should always be turned on for Undump
 
 my @input = ( {foo=>"bar"}, ["Hello","World"], "undumping!" );
 my $str = Dumper(@input);          # dump the data structure to a string
 my @parsed = Undump($str);         # parse the data structure back out
 
 # @parsed now looks identical to @input (is a deep copy)

=head1 Description

This module allows for I<limited> undumping and round-tripping of data
structures from strings generated by L<Data::Dumper|Data::Dumper>
(and possibly other dumper modules, but that's currently not explicitly supported).
It is a thin wrapper around L<Config::Perl|Config::Perl>, so please
see L<Config::Perl> for more details, including the limitations.

B<< This module exports a single function, C<Undump>, >> which accepts a
string and attempts to return the data as it would have been passed to
L<Data::Dumper|Data::Dumper>'s C<Dumper>. This means that the C<$VAR1>,
C<$VAR2> etc. variable names generated by C<Dumper> will be removed and the
argument list passed to C<Dumper(...)> is returned, and if the string ends
on a true value, that will be ignored so that parsing files which end on
e.g. "<c>1;</c>" will work. If the string doesn't look like the output of
L<Data::Dumper|Data::Dumper>, this function will throw an exception, and
any errors from L<Config::Perl|Config::Perl>'s C<parse_or_die> will be
passed through as well.

B<< When using L<Data::Dumper|Data::Dumper>, >> make sure to always turn on its
C<Purity> option and turn off its C<Terse> option,
as otherwise L<Data::Dumper|Data::Dumper> may produce code that may not evaluate
back to the same data structure, sometimes even though it's valid, parseable Perl!

This module aims to support most of L<Data::Dumper|Data::Dumper>'s features
except code references and C<bless>ed objects.
If you find a L<Data::Dumper|Data::Dumper> data structure that this module
does not yet support, please feel free to send in your data structure, as
it can help extend L<Config::Perl|Config::Perl>'s features and help fix bugs.
Currently, using modules other than L<Data::Dumper|Data::Dumper> may not work,
for example, L<Data::Dump|Data::Dump> sometimes generates code with the C<..>
range operator, which is currently not supported by L<Config::Perl|Config::Perl>.
In the future, this module's features may be extended to more fully support
dumper modules like L<Data::Dump|Data::Dump> as well.

Although L<Config::Perl|Config::Perl> now supports self-referential data
structures, you can also use L<Data::Dumper|Data::Dumper>'s C<Deepcopy>
option to get rid of references within data structures,
if the loss of references and copying of data is acceptable for your application.

This module is part of the L<Config::Perl|Config::Perl> distribution,
but was named separately in an attempt to make its purpose more clear
and its name a little easier to remember.

This document describes version 0.05 of the module.
B<This is a development version.>
Although this module has a fair number of tests, it still lacks some
features (see L<Config::Perl|Config::Perl>) and there may be bugs lurking.
Contributions are welcome!

=head1 Author, Copyright, and License

Copyright (c) 2015 Hauke Daempfling (haukex@zero-g.net).

This library is free software; you can redistribute it and/or modify
it under the same terms as Perl 5 itself.

For more information see the L<Perl Artistic License|perlartistic>,
which should have been distributed with your copy of Perl.
Try the command "C<perldoc perlartistic>" or see
L<http://perldoc.perl.org/perlartistic.html>.

=cut

use Carp;
use Exporter 'import';

our @EXPORT = qw(Undump);  ## no critic (ProhibitAutomaticExportation)

use Config::Perl;

sub Undump {
	my $in = shift;
	warnings::warnif('Config::Perl',"ignoring extra arguments to Undump") if @_;
	my $parsed = Config::Perl->new->parse_or_die(\$in);
	my $data_dumper=1; # does this look like Data::Dumper output?
	/^(?:\$VAR\d+|_)$/ or $data_dumper=0 for keys %$parsed;
	if (exists $$parsed{_}) {
		$data_dumper=0 unless @{ $$parsed{_} }==1 && $$parsed{_}[0];
		delete $$parsed{_};
	}
	croak "string doesn't look like Data::Dumper output"
		unless $data_dumper;
	# sort the $VAR\d+ variables correctly
	return
		map { $$parsed{ $$_[0] } }
		sort { $$a[1] <=> $$b[1] }
		map { [$_, /^\$VAR(\d+)$/] }
		keys %$parsed;
}


1;

